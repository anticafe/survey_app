apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs.kotlin"

android {
  compileSdkVersion 29
  buildToolsVersion "29.0.2"
  defaultConfig {
    applicationId "com.surveyapp"
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    javaCompileOptions {
      annotationProcessorOptions {
        arguments << [
            "room.schemaLocation": "$projectDir/schemas".toString(),
            "room.incremental": "true",
            "room.expandProjection": "true"
        ]
      }
    }
  }

  lintOptions {
    // If set to true, turns off analysis progress reporting by lint.
    // quiet true
    // if set to true (default), stops the build if errors are found.
    abortOnError true
    // if true, only report errors.
    ignoreWarnings true
    lintConfig file('lint.xml')
  }

  packagingOptions {
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude '.readme'
    exclude 'META-INF/atomicfu.kotlin_module'
  }

  testOptions {
    unitTests.includeAndroidResources = true
  }

  buildTypes {
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  productFlavors.whenObjectAdded { flavor ->
    println('productFlavors of ${flavor}')
    def flavorData = rootProject.ext[flavor.name]

    flavor.applicationId flavorData.appId

    flavor.buildConfigField 'String', strEndpointUrl, "\"" + flavorData.endpointUrl + "\""
    flavor.buildConfigField 'String', strAccountGrantType, "\"" + flavorData.accountGrantType + "\""
    flavor.buildConfigField 'String', strAccountDefaultUsername,
        "\"" + flavorData.accountDefaultUsername + "\""
    flavor.buildConfigField 'String', strAccountDefaultPassword,
        "\"" + flavorData.accountDefaultPassword + "\""
  }
  flavorDimensions "environment"
  productFlavors {
    // Temporarily don't use FAKE flavor
    // fake {}
    dev {}
    prod {}
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'androidx.core:core-ktx:1.1.0'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

  def lifecycle_version = "2.2.0"
  implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
  implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

  def coroutines_version = "1.3.3"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

  def room_version = "2.2.3"
  implementation "androidx.room:room-runtime:$room_version"
  // Use annotationProcessor instead kapt which will cause compile error
  kapt "androidx.room:room-compiler:$room_version"
  implementation "androidx.room:room-ktx:$room_version"

  implementation "androidx.viewpager2:viewpager2:1.0.0"
  implementation("io.coil-kt:coil:0.9.3")
  implementation 'me.relex:circleindicator:2.1.4'
  // navigation component
  implementation "androidx.navigation:navigation-fragment-ktx:2.2.0"
  implementation "androidx.navigation:navigation-ui-ktx:2.2.0"

  implementation "com.google.code.gson:gson:2.8.6"
  implementation "com.squareup.retrofit2:retrofit:2.7.1"
  implementation "com.squareup.retrofit2:converter-gson:2.7.1"
  implementation "com.squareup.okhttp3:logging-interceptor:4.3.1"

  // Dagger
  def dagger_version = "2.26"
  implementation "com.google.dagger:dagger:$dagger_version"
  kapt "com.google.dagger:dagger-compiler:$dagger_version"
  implementation "com.google.dagger:dagger-android-support:$dagger_version"
  kapt "com.google.dagger:dagger-android-processor:$dagger_version"
  // Using Dagger in androidTest and Robolectric too
  kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
  kaptTest "com.google.dagger:dagger-compiler:$dagger_version"

  implementation "com.jakewharton.timber:timber:4.7.1"

  // debugImplementation because LeakCanary should only run in debug builds.
  debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'

  // Note: DON'T upgrade JUnit to v4.13 - it will cause error "Unresolved reference: Test" in
  // test file although these test cases can still be run
  //noinspection GradleDependency
  testImplementation 'junit:junit:4.12'
  // Room test helpers
  testImplementation "androidx.room:room-testing:$room_version"
  testImplementation 'org.robolectric:robolectric:4.3.1'
  // Optional -- Robolectric environment
  testImplementation "androidx.test:core:1.2.0"
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

  testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
  testImplementation "org.mockito:mockito-core:3.2.4"
  // Use mockk v1.9.3 instead 1.9.3.kotlin12
  testImplementation "io.mockk:mockk:1.9.3"

  def androidX_core_test_version = "2.1.0"
  testImplementation "androidx.arch.core:core-testing:$androidX_core_test_version"

  testImplementation 'com.google.truth:truth:1.0.1'

  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
